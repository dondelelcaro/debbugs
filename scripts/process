#!/usr/bin/perl
# $Id: process.in,v 1.109 2006/02/09 22:02:04 don Exp $
#
# Usage: process nn
# Temps:  incoming/Pnn

use warnings;
use strict;

use POSIX qw(strftime);

use IO::File;

use MIME::Parser;
use Debbugs::MIME qw(decode_rfc1522 create_mime_message getmailbody);
use Debbugs::Mail qw(send_mail_message encode_headers);
use Debbugs::Packages qw(getpkgsrc);
use Debbugs::User qw(read_usertags write_usertags);
use Debbugs::Common qw(:lock get_hashname);
use Debbugs::Status qw(writebug isstrongseverity lockreadbugmerge lockreadbug);

use Debbugs::CGI qw(html_escape bug_url);

use Debbugs::Log qw(:misc);

use Debbugs::Text qw(:templates);

use Debbugs::Status qw(:versions);
use Debbugs::Config qw(:globals :config);

chdir( "$gSpoolDir" ) || die "chdir spool: $!\n";

#open(DEBUG,"> /tmp/debbugs.debug");
umask(002);
open DEBUG, ">/dev/null";

my $intdate = time or die "failed to get time: $!";

$_=shift;
m/^([BMQFDUL])(\d*)\.\d+$/ or die "bad argument: $_";
my $codeletter= $1;
my $tryref= length($2) ? $2 : -1;
my $nn= $_;

if (!rename("incoming/G$nn","incoming/P$nn")) 
{
    $_=$!.'';  m/no such file or directory/i && exit 0;
    die "renaming to lock: $!";
}

my $baddress= 'submit' if $codeletter eq 'B';
$baddress= 'maintonly' if $codeletter eq 'M';
$baddress= 'quiet' if $codeletter eq 'Q';
$baddress= 'forwarded' if $codeletter eq 'F';
$baddress= 'done' if $codeletter eq 'D';
$baddress= 'submitter' if $codeletter eq 'U';
bug_list_forward($nn) if $codeletter eq 'L';
$baddress || die "bad codeletter $codeletter";
my $baddressroot= $baddress;
$baddress= "$tryref-$baddress" if $tryref>=0;

open(M,"incoming/P$nn");
my @log=<M>;
close(M);

my @msg = @log;
chomp @msg;

print DEBUG "###\n",join("##\n",@msg),"\n###\n";

my $tdate = strftime "%a, %d %h %Y %T +0000", gmtime;
my $fwd= <<END;
Received: via spool by $baddress\@$gEmailDomain id=$nn
          (code $codeletter ref $tryref); $tdate
END

# header and decoded body respectively
my (@headerlines, @bodylines);

# whether maintainer addresses have been checked
our $maintainerschecked = 0;
#maintainer address for this message
our @maintaddrs;
# other src addresses
our @addsrcaddrs;
our @resentccs;
our @bccs;

my $resentccexplain='';

# whether there's a new reference with this email
our $newref = 0;

our $brokenness = '';

my $parser = new MIME::Parser;
mkdir "$gSpoolDir/mime.tmp", 0777;
$parser->output_under("$gSpoolDir/mime.tmp");
my $entity = eval { $parser->parse_data(join('',@log)) };

my $i;
if ($entity and $entity->head->tags) {
    @headerlines = @{$entity->head->header};
    chomp @headerlines;

    my $entity_body = getmailbody($entity);
    @bodylines = map {s/\r?\n$//; $_;}
	 $entity_body ? $entity_body->as_lines() : ();

    # set $i to beginning of encoded body data, so we can dump it out
    # verbatim later
    $i = 0;
    ++$i while $msg[$i] =~ /./;
} else {
    # Legacy pre-MIME code, kept around in case MIME::Parser fails.
    for ($i = 0; $i <= $#msg; $i++) {
	$_ = $msg[$i];
	last unless length($_);
	while ($msg[$i+1] =~ m/^\s/) {
	    $i++;
	    $_ .= "\n".$msg[$i];
	}
	push @headerlines, $_;
    }

    @bodylines = @msg[$i..$#msg];
}

my %header;

for my $hdr (@headerlines) {
    $hdr = decode_rfc1522($hdr);
    $_ = $hdr;
    s/\n\s/ /g;
    &finish if m/^x-loop: (\S+)$/i && $1 eq "$gMaintainerEmail";
    my $ins = !m/^subject:/i && !m/^reply-to:/i && !m/^return-path:/i
           && !m/^From / && !m/^X-Debbugs-/i;
    $fwd .= $hdr."\n" if $ins;
    # print DEBUG ">$_<\n";
    if (s/^(\S+):\s*//) {
	my $v = lc $1;
	print DEBUG ">$v=$_<\n";
	$header{$v} = $_;
    } else {
	print DEBUG "!>$_<\n";
    }
}
$header{'message-id'} = '' if not defined $header{'message-id'};

# remove blank lines
shift @bodylines while @bodylines and $bodylines[0] !~ /\S/;

# Strip off RFC2440-style PGP clearsigning.
if (@bodylines and $bodylines[0] =~ /^-----BEGIN PGP SIGNED/) {
    shift @bodylines while @bodylines and length $bodylines[0];
    shift @bodylines while @bodylines and $bodylines[0] !~ /\S/;
    for my $findsig (0 .. $#bodylines) {
	if ($bodylines[$findsig] =~ /^-----BEGIN PGP SIGNATURE/) {
	    $#bodylines = $findsig - 1;
	    last;
	}
    }
    map { s/^- // } @bodylines;
}

#psuedoheaders
my %pheader;
# extract pseudo-headers
for my $phline (@bodylines)
{
    last if $phline !~ m/^([\w-]+):\s*(\S.*)/;
    my ($fn, $fv) = ($1, $2);
    $fv =~ s/\s*$//;
    print DEBUG ">$fn|$fv|\n";
    $fn = lc $fn;
    # Don't lc owner or forwarded
    $fv = lc $fv unless $fn =~ /^(?:owner|forwarded|usertags|version|source-version)$/;
    $pheader{$fn} = $fv;
    print DEBUG ">$fn~$fv<\n";
}

# Allow pseudo headers to set x-debbugs- stuff [#179340]
for my $key (grep /X-Debbugs-.*/i, keys %pheader) {
     $header{$key} = $pheader{$key} if not exists $header{$key};
}

$fwd .= join("\n",@msg[$i..$#msg]);

print DEBUG "***\n$fwd\n***\n";

if (defined $header{'resent-from'} && !defined $header{'from'}) {
    $header{'from'} = $header{'resent-from'};
}
defined($header{'from'}) || die "no From header";

my $replyto = $header{'reply-to'};
$replyto = '' unless defined $replyto;
$replyto =~ s/^ +//;
$replyto =~ s/ +$//;
unless (length $replyto) {
    $replyto = $header{'from'};
}

my $subject = '(no subject)';
if (!defined($header{'subject'})) 
{
	$brokenness.= fill_template('mail/process_broken_subject');

} else { 
    $subject= $header{'subject'}; 
}

my $ref=-1;
$subject =~ s/^Re:\s*//i; $_= $subject."\n";
if ($tryref < 0 && m/^Bug ?\#(\d+)\D/i) {
    $tryref= $1+0; 
}
my $data;
if ($tryref >= 0) 
{
     my $bfound;
    ($bfound, $data)= &lockreadbugmerge($tryref);
    if ($bfound) { 
        $ref= $tryref; 
    } else {
        &htmllog("Reply","sent", $replyto,"Unknown problem report number <code>$tryref</code>.");
        &sendmessage(create_mime_message(
          [From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
	   To            => $replyto,
	   Subject       => "Unknown problem report $gBug#$tryref ($subject)",
	   'Message-ID'  => "<handler.x.$nn.unknown\@$gEmailDomain>",
	   'In-Reply-To' => $header{'message-id'},
	   References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
	   Precedence    => 'bulk',
	   "X-$gProject-PR-Message" => 'error',
	  ],message_body_template('process_unknown_bug_number',
				  {subject => $subject,
				   date    => $header{date},
				   baddress => $baddress,
				   tryref   => $tryref,
				   messageid => $header{'message-id'},
				  },
				 )),'');
        &appendlog;
        &finish;
    }
} else { 
    &filelock('lock/-1'); 
}

# Attempt to determine which source package this is
my $source_pr_header = '';
my $source_package = '';
if (defined $pheader{source}) {
     $source_package = $pheader{source};
}
elsif (defined $data->{package} or defined $pheader{package}) {
     my $pkg_src = getpkgsrc();
     $source_package = $pkg_src->{defined $data->{package}?$data->{package}:$pheader{package}};
}
$source_pr_header = "X-$gProject-PR-Source: $source_package\n"
     if defined $source_package and length $source_package;

# Done and Forwarded Bugs
if ($codeletter eq 'D' || $codeletter eq 'F') 
{
    if ($replyto =~ m/$gBounceFroms/o ||
        $header{'from'} =~ m/$gBounceFroms/o)
    {
	 print STDERR "bounce detected !  Mwaap! Mwaap!";
	 exit 1;
    }
    my $markedby= $header{'from'} eq $replyto ? $replyto :
               "$header{'from'} (reply to $replyto)";
    my @generalcc;
    my $receivedat;
    my $markaswhat;
    my $set_forwarded;
    my $generalcc;
    my $set_done;
    if ($codeletter eq 'F') { # Forwarded
        (&appendlog,&finish) if defined $data->{forwarded} and length($data->{forwarded});
        $receivedat= "forwarded\@$gEmailDomain";
        $markaswhat= 'forwarded';
        $set_forwarded= $header{'to'};
	# Dissallow forwarded being set to this bug tracking system
	if (defined $set_forwarded and $set_forwarded =~ /\Q$gEmailDomain\E/) {
	     undef $set_forwarded;
	}
	if ( length( $gListDomain ) > 0 && length( $gForwardList ) > 0 ) {
	    push @generalcc, "$gForwardList\@$gListDomain";
	    $generalcc= "$gForwardList\@$gListDomain";
	} else { 
	    $generalcc=''; 
        }
    } else { # Done
        if (defined $data->{done} and length($data->{done}) and
                not defined $pheader{'source-version'} and
                not defined $pheader{'version'}) {
            &appendlog;
            &finish;
        }
        $receivedat= "done\@$gEmailDomain";
        $markaswhat= 'done';
        $set_done= $header{'from'};
	if ( length( $gListDomain ) > 0 && length( $gDoneList ) > 0 ) {
            $generalcc= "$gDoneList\@$gListDomain";
	    push @generalcc, "$gDoneList\@$gListDomain";
	} else { 
	    $generalcc=''; 
	}
    }
    if (defined $gStrongList and isstrongseverity($data->{severity})) {
        $generalcc = join ', ', $generalcc, "$gStrongList\@$gListDomain";
	push @generalcc,"$gStrongList\@$gListDomain";
    }
    if ($ref<0) {
	&htmllog("Warning","sent",$replyto,"Message ignored.");
	&sendmessage(create_mime_message(
          [From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
	   To            => $replyto,
	   Subject       => "Message with no $gBug number ignored by $receivedat ($subject)",
	   'Message-ID'  => "<handler.x.$nn.warnignore\@$gEmailDomain>",
	   'In-Reply-To' => $header{'message-id'},
	   References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
	   Precedence    => 'bulk',
	   "X-$gProject-PR-Message" => 'error',
	  ],message_body_template('mail/process_no_bug_number',
				  {subject => $subject,
				   date    => $header{date},
				   markaswhat => $markaswhat,
				   receivedat => $receivedat,
				   messageid => $header{'message-id'},
				  },
				 )),'');
	&appendlog;
	&finish;
    }

    &checkmaintainers;

    my @noticecc = grep($_ ne $replyto,@maintaddrs);
    my $noticeccval.= join(', ', grep($_ ne $replyto,@maintaddrs));
    $noticeccval =~ s/\s+\n\s+/ /g; 
    $noticeccval =~ s/^\s+/ /; $noticeccval =~ s/\s+$//;

    my @process= ($ref,split(/ /,$data->{mergedwith}));
    my $orgref= $ref;

    for $ref (@process) {
 	if ($ref != $orgref) {
	    &unfilelock;
	    $data = &lockreadbug($ref)
		|| die "huh ? $ref from $orgref out of ".join(' ',@process);
	}
        $data->{done}= $set_done if defined($set_done);
        $data->{forwarded}= $set_forwarded if defined($set_forwarded);
        if ($codeletter eq 'D') {
            $data->{keywords} = join ' ', grep $_ ne 'pending',
                                     split ' ', $data->{keywords};
            if (defined $pheader{'source-version'}) {
		 if ($pheader{'source-version'} !~ m/^$config{package_version_re}$/) {
		      $brokenness .= fill_template('mail/invalid_version',
						   {version => $pheader{'source-version'}},
						  );
		 }
		 else {
		      addfixedversions($data, $pheader{source}, $pheader{'source-version'}, '');
		 }
	    } elsif (defined $pheader{version}) {
		 if ($pheader{version} !~ m/^$config{package_version_re}$/) {
		      $brokenness .= fill_template('mail/invalid_version',
						   {version => $pheader{version}},
						  );
		 }
		 else {
		      addfixedversions($data, $pheader{package}, $pheader{version}, '');
		 }
	    }
        }

	# Add bug mailing list to $generalbcc as appropriate
 	# This array is used to specify bcc in the cases where we're using create_mime_message.
 	my @generalbcc = (@generalcc,@addsrcaddrs,"bugs=$ref\@$gListDomain");
	my $generalbcc = join(', ', $generalcc, @addsrcaddrs,"bugs=$ref\@$gListDomain");
	$generalbcc =~ s/\s+\n\s+/ /g;
	$generalbcc =~ s/^\s+/ /; $generalbcc =~ s/\s+$//;
	if (length $generalbcc) {$generalbcc = "Bcc: $generalbcc\n"};

	writebug($ref, $data);

	my $hash = get_hashname($ref);
        open(O,"db-h/$hash/$ref.report") || die "read original report: $!";
        my $orig_report= join('',<O>); close(O);
        if ($codeletter eq 'F') {
	    &htmllog("Reply","sent",$replyto,"You have marked $gBug as forwarded.");
            &sendmessage(create_mime_message(
	     ["X-Loop"      => "$gMaintainerEmail",
	      From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
              To            => "$replyto",
              Subject       => "$gBug#$ref: marked as forwarded ($data->{subject})",
              "Message-ID"  => "<header.$ref.$nn.ackfwdd\@$gEmailDomain>",
              "In-Reply-To" => $header{'message-id'},
              References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
              Precedence    => 'bulk',
              "X-$gProject-PR-Message"  => "forwarded $ref",
              "X-$gProject-PR-Package"  => $data->{package},
              "X-$gProject-PR-Keywords" => $data->{keywords},
	      # Only have a X-$gProject-PR-Source when we know the source package
	      (defined($source_package) and length($source_package))?("X-$gProject-PR-Source" => $source_package):(),
             ],message_body_template('mail/process_mark_as_forwarded',
				     {date => $header{date},
				      messageid => $header{'message-id'},
				      data      => $data,
				     },
				    ),
	     [join("\n",@msg)]),'',[$replyto,@generalbcc,@noticecc],1);
        } else {
	    &htmllog("Reply","sent",$replyto,"You have taken responsibility.");
            &sendmessage(create_mime_message(
	     ["X-Loop"      => "$gMaintainerEmail",
	      From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
              To            => $replyto,
              Subject       => "$gBug#$ref: marked as done ($data->{subject})",
              "Message-ID"  => "<handler.$ref.$nn.ackdone\@$gEmailDomain>",
              "In-Reply-To" => $header{'message-id'},
              References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
              Precedence    => 'bulk',
              "X-$gProject-PR-Message"  => "closed $ref",
              "X-$gProject-PR-Package"  => $data->{package},
              "X-$gProject-PR-Keywords" => $data->{keywords},
	      # Only have a X-$gProject-PR-Source when we know the source package
	      (defined($source_package) and length($source_package))?("X-$gProject-PR-Source" => $source_package):(),
             ],message_body_template('mail/process_mark_as_done',
				     {date => $header{date},
				      messageid => $header{'message-id'},
				      subject   => $header{subject},
				      data      => $data,
				     },
				    ),
	     [$orig_report,join("\n",@msg)]),'',[$replyto,@generalbcc,@noticecc],1);
            &htmllog("Notification","sent",$data->{originator},
		"$gBug acknowledged by developer.");
            &sendmessage(create_mime_message(
	     ["X-Loop"      => "$gMaintainerEmail",
	      From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
              To            => "$data->{originator}",
              Subject       => "$gBug#$ref closed by $markedby ($header{'subject'})",
              "Message-ID"  => "<handler.$ref.$nn.notifdone\@$gEmailDomain>",
              "In-Reply-To" => "$data->{msgid}",
              References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
              "X-$gProject-PR-Message"  => "they-closed $ref",
              "X-$gProject-PR-Package"  => "$data->{package}",
              "X-$gProject-PR-Keywords" => "$data->{keywords}",
	      # Only have a X-$gProject-PR-Source when we know the source package
	      (defined($source_package) and length($source_package))?("X-$gProject-PR-Source" => $source_package):(),
              "Reply-To"                => "$ref\@$gEmailDomain",
              "Content-Type"            => 'text/plain; charset="utf-8"',
             ],message_body_template('mail/process_your_bug_done',
				     {data      => $data,
				      markedby  => $markedby,
				      messageid => $header{'message-id'},
				      subject   => $header{subject},
				     },
				    ),
	     [join("\n",@msg),$orig_report]),'',undef,1);
        }
	&appendlog;
    }
    &finish;
}

if ($ref<0) { # new bug report
    if ($codeletter eq 'U') { # -submitter
        &htmllog("Warning","sent",$replyto,"Message not forwarded.");
	&sendmessage(create_mime_message(
          [From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
	   To            => $replyto,
	   Subject       => "Message with no $gBug number cannot be sent to submitter! ($subject)",
	   'Message-ID'  => "<handler.x.$nn.nonumnosub\@$gEmailDomain>",
	   'In-Reply-To' => $header{'message-id'},
	   References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
	   Precedence    => 'bulk',
	   "X-$gProject-PR-Message" => 'error',
	  ],message_body_template('mail/process_no_bug_number',
				  {subject => $subject,
				   date    => $header{date},
				   markaswhat => 'submitter',
				   receivedat => "$baddress\@$gEmailDomain",
				   messageid => $header{'message-id'},
				  },
				 )),'');
	&appendlog;
	&finish;
    }

    $data->{found_versions} = [];
    $data->{fixed_versions} = [];

    if (defined $pheader{source}) {
        $data->{package} = $pheader{source};
    } elsif (defined $pheader{package}) {
        $data->{package} = $pheader{package};
    } elsif (defined $config{default_package}) {
	$data->{package} = $config{default_package},
    }
    else {
 	&htmllog("Warning","sent",$replyto,"Message not forwarded.");
	my $body = message_body_template('mail/process_no_package',
					);
        &sendmessage(create_mime_message(
                       ["X-Loop"      => "$gMaintainerEmail",
			From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
                        To            => $replyto,
                        Subject       => "Message with no Package: tag cannot be processed! ($subject)",
                        "Message-ID"  => "<handler.x.$nn.nonumnosub\@$gEmailDomain>",
                        "In-Reply-To" => $header{'message-id'},
                        References    => join(' ',grep {defined $_} $header{'message-id'},$data->{msgid}),
                        Precedence    => 'bulk',
                        "X-$gProject-PR-Message" => 'error'
		       ],
	   message_body_template('mail/process_no_package',
				 {date => $header{date},
				  subject => $subject,
				  messageid => $header{'message-id'},
				  baddress => $baddress,
				 },
				),[join("\n", @msg)]), '',undef,1);
	&appendlog;
	&finish;
    }

    if (defined $config{default_package}) {
	 &checkmaintainers;
	 # if there are no maintainers for this package, assign it to the default package
	 if (not @maintaddrs) {
	      $data->{package} = $config{default_package};
	      $brokenness.= fill_template('mail/process_default_package_selected',
					  {old_package => $pheader{source} || $pheader{package} || 'No package',
					   new_package => $data->{package},
					  }
					 );
	      # force the maintainers to be rechecked
	      $maintainerschecked = 0;
	      &checkmaintainers;
	 }
    }

    $data->{keywords}= '';
    if (defined($pheader{'keywords'})) {
        $data->{keywords}= $pheader{'keywords'};
    } elsif (defined($pheader{'tags'})) {
        $data->{keywords}= $pheader{'tags'};
    }
    if (length($data->{keywords})) {
        my @kws;
        my %gkws = map { ($_, 1) } @gTags;
        foreach my $kw (sort split(/[,\s]+/, lc($data->{keywords}))) {
            push @kws, $kw if (defined $gkws{$kw});
        }
        $data->{keywords} = join(" ", @kws);
    }
    $data->{severity}= '';
    if (defined($pheader{'severity'}) || defined($pheader{'priority'})) {
 	$data->{severity}= $pheader{'severity'};
	$data->{severity}= $pheader{'priority'} unless ($data->{severity});
	$data->{severity} =~ s/^\s*(.+)\s*$/$1/;

	if (!grep($_ eq $data->{severity}, @gSeverityList, "$gDefaultSeverity")) {
            $brokenness.= fill_template('mail/invalid_severity',
					{severity=>$data->{severity}}
				       );
            $data->{severity}= '';
        }
    }
    if (defined($pheader{owner})) {
        $data->{owner}= $pheader{owner};
    }
    if (defined($pheader{forwarded})) {
	$data->{'forwarded-to'} = $pheader{forwarded};
    }
    &filelock("nextnumber.lock");
    open(N,"nextnumber") || die "nextnumber: read: $!";
    my $nextnumber=<N>; $nextnumber =~ s/\n$// || die "nextnumber bad format";
    $ref= $nextnumber+0;  $nextnumber += 1;  $newref=1;
    &overwrite('nextnumber', "$nextnumber\n");
    &unfilelock;
    my $hash = get_hashname($ref);
    &overwrite("db-h/$hash/$ref.log",'');
    $data->{originator} = $replyto;
    $data->{date} = $intdate;
    $data->{subject} = $subject;
    $data->{msgid} = $header{'message-id'};
    writebug($ref, $data);
    # Deal with usertags
    if (exists $pheader{usertags}) {
	 my $user = $replyto;
	 $user = $pheader{user} if exists $pheader{user};
	 $user =~ s/,.*//;
	 $user =~ s/^.*<(.*)>.*$/$1/;
	 $user =~ s/[(].*[)]//;
	 $user =~ s/^\s*(\S+)\s+.*$/$1/;
	 if ($user ne '' and Debbugs::User::is_valid_user($user)) {
	      $pheader{usertags} =~ s/(?:^\s+|\s+$)//g;
	      my %user_tags;
	      read_usertags(\%user_tags,$user);
	      for my $tag (split /[,\s]+/, $pheader{usertags}) {
		   if ($tag =~ /^[a-zA-Z0-9.+\@-]+/) {
			my %bugs_with_tag; 
			@bugs_with_tag{@{$user_tags{$tag}||[]}} = (1) x @{$user_tags{$tag}||[]};
			$bugs_with_tag{$ref} = 1;
			$user_tags{$tag} = [keys %bugs_with_tag];
		   }
	      }
	      write_usertags(\%user_tags,$user);
	 }
	 else {
	      $brokenness .= fill_template('mail/invalid_user',
					   {user => $user}
					  );
	 }
    }
    &overwrite("db-h/$hash/$ref.report",
               join("\n",@msg)."\n");
}

&checkmaintainers;

print DEBUG "maintainers >".join(' ',@maintaddrs)."<\n";

my $orgsender= defined($header{'sender'}) ? "Original-Sender: $header{'sender'}\n" : '';
my $newsubject= $subject;  $newsubject =~ s/^$gBug#$ref:*\s*//;

my $xcchdr= $header{ 'x-debbugs-cc' } || '';
if ($xcchdr =~ m/\S/) {
    push(@resentccs,$xcchdr);
    $resentccexplain.= fill_template('mail/xdebbugscc',
				     {xcchdr => $xcchdr},
				    );
}

if (@maintaddrs && ($codeletter eq 'B' || $codeletter eq 'M')) {
    push(@resentccs,@maintaddrs);
    $resentccexplain.= fill_template('mail/maintainercc',
				     {maintaddrs => \@maintaddrs,
				     },
				    );
}

@bccs = @addsrcaddrs;
if (defined $gStrongList and isstrongseverity($data->{severity})) {
    push @bccs, "$gStrongList\@$gListDomain";
}

# Send mail to the per bug list subscription too
push @bccs, "bugs=$ref\@$gListDomain";

if (defined $pheader{source}) {
    # Prefix source versions with the name of the source package. They
    # appear that way in version trees so that we can deal with binary
    # packages moving from one source package to another.
    if (defined $pheader{'source-version'}) {
	 if ($pheader{'source-version'} !~ m/^$config{package_version_re}$/) {
	      $brokenness .= fill_template('mail/invalid_version',
					   {version => $pheader{'source-version'}},
					  );
	 }
	 else {
	      addfoundversions($data, $pheader{source}, $pheader{'source-version'}, '');
	 }
    } elsif (defined $pheader{version}) {
	 if ($pheader{version} !~ m/^$config{package_version_re}$/) {
	      $brokenness .= fill_template('mail/invalid_version',
					   {version => $pheader{version}},
					  );
	 }
	 else {
	      addfoundversions($data, $pheader{source}, $pheader{version}, '');
	 }
    }
    writebug($ref, $data);
} elsif (defined $pheader{package}) {
    # TODO: could handle Source-Version: by looking up the source package?
     if (defined $pheader{version}) {
	  if ($pheader{version} !~ m/^$config{package_version_re}$/) {
	       $brokenness .= fill_template('mail/invalid_version',
					    {version => $pheader{version}},
					   );
	  }
	  else {
	       addfoundversions($data, $pheader{package}, $pheader{version}, 'binary');
	  }
     }
     writebug($ref, $data);
}

my $veryquiet= $codeletter eq 'Q';
if ($codeletter eq 'M' && !@maintaddrs) {
    $veryquiet= 1;
    $brokenness.= fill_template('mail/invalid_maintainer',
				{},
			       );
}

my $resentccval.= join(', ',@resentccs);
$resentccval =~ s/\s+\n\s+/ /g; $resentccval =~ s/^\s+/ /; $resentccval =~ s/\s+$//;
my $resentcc = '';
if (length($resentccval)) { 
    $resentcc= "Resent-CC: $resentccval\n"; 
}

if ($codeletter eq 'U') { # sent to -submitter
    &htmllog("Message", "sent on", $data->{originator}, "$gBug#$ref.");
    &sendmessage(<<END,[$data->{originator},@resentccs],[@bccs]);
Subject: $gBug#$ref: $newsubject
Reply-To: $replyto, $ref-quiet\@$gEmailDomain
${orgsender}Resent-To: $data->{originator}
${resentcc}Resent-Date: $tdate
Resent-Message-ID: <handler.$ref.$nn\@$gEmailDomain>
Resent-Sender: $gMaintainerEmail
X-$gProject-PR-Message: report $ref
X-$gProject-PR-Package: $data->{package}
X-$gProject-PR-Keywords: $data->{keywords}
${source_pr_header}$fwd
END
} elsif ($codeletter eq 'B') { # Sent to submit
    my $report_followup = $newref ? 'report' : 'followup';
    &htmllog($newref ? "Report" : "Information", "forwarded",
             join(', ',"$gSubmitList\@$gListDomain",@resentccs),
             "<code>$gBug#$ref</code>".
             (length($data->{package})? "; Package <code>".html_escape($data->{package})."</code>" : '').
             ".");
    &sendmessage(<<END,["$gSubmitList\@$gListDomain",@resentccs],[@bccs]);
Subject: $gBug#$ref: $newsubject
Reply-To: $replyto, $ref\@$gEmailDomain
Resent-From: $header{'from'}
${orgsender}Resent-To: $gSubmitList\@$gListDomain
${resentcc}Resent-Date: $tdate
Resent-Message-ID: <handler.$ref.$nn\@$gEmailDomain>
Resent-Sender: $gMaintainerEmail
X-$gProject-PR-Message: $report_followup $ref
X-$gProject-PR-Package: $data->{package}
X-$gProject-PR-Keywords: $data->{keywords}
${source_pr_header}$fwd
END
} elsif (@resentccs or @bccs) { # Quiet or Maintainer
    # D and F done far earlier; B just done - so this must be M or Q
    # We preserve whichever it was in the Reply-To (possibly adding
    # the $gBug#).
    my $report_followup = $newref ? 'report' : 'followup';
    if (@resentccs) {
        &htmllog($newref ? "Report" : "Information", "forwarded",
                 $resentccval,
                 "<code>$gBug#$ref</code>".
                 (length($data->{package}) ? "; Package <code>".html_escape($data->{package})."</code>" : '').
                 ".");
    } else {
        &htmllog($newref ? "Report" : "Information", "stored",
                 "",
                 "<code>$gBug#$ref</code>".
                 (length($data->{package}) ? "; Package <code>".html_escape($data->{package})."</code>" : '').
                 ".");
    }
    &sendmessage(<<END,[@resentccs],[@bccs]);
Subject: $gBug#$ref: $newsubject
Reply-To: $replyto, $ref-$baddressroot\@$gEmailDomain
Resent-From: $header{'from'}
${orgsender}Resent-To: $resentccval
Resent-Date: $tdate
Resent-Message-ID: <handler.$ref.$nn\@$gEmailDomain>
Resent-Sender: $gMaintainerEmail
X-$gProject-PR-Message: $report_followup $ref
X-$gProject-PR-Package: $data->{package}
X-$gProject-PR-Keywords: $data->{keywords}
${source_pr_header}$fwd
END
}

my $htmlbreak= length($brokenness) ? "<p>\n".html_escape($brokenness)."\n<p>\n" : '';
$htmlbreak =~ s/\n\n/\n<P>\n\n/g;
if (length($resentccval)) {
    $htmlbreak = "  Copy sent to <code>".html_escape($resentccval)."</code>.".
        $htmlbreak;
}

# Should we send an ack out?
if (not exists $header{'x-debbugs-no-ack'} and
    ($newref or
     ($codeletter ne 'U' and
      (not defined $header{precedence} or
       $header{'precedence'} !~ /\b(?:bulk|junk|list)\b/
      )
     )
    )
   ){

     # figure out forward explanation
     my $forwardexplain = '';
     my $thanks = '';
     my $extra_vars;
     # will contain info and -info in moreinfo messages
     my $info = '';
     my $infod = '';
     # temporary headers
     my %t_h;
     if ($newref) {
	  &htmllog("Acknowledgement","sent",$replyto,
		   ($veryquiet ?
		    "New $gBug report received and filed, but not forwarded." :
		    "New $gBug report received and forwarded."). $htmlbreak);
	  $thanks = fill_template('mail/process_ack_thanks_new');
     }
     else {
	  &htmllog("Acknowledgement","sent",$replyto,
		   ($veryquiet ? "Extra info received and filed, but not forwarded." :
		    $codeletter eq 'M' ? "Extra info received and forwarded to maintainer." :
		    "Extra info received and forwarded to list."). $htmlbreak);
	  $thanks = fill_template('mail/process_ack_thanks_additional');
	  $info = 'info';
	  $infod = '-info';
     }
     if ($veryquiet) {
	  $forwardexplain = fill_template('mail/forward_veryquiet',
					 );
	  # these are the headers that quiet messages override
	  $t_h{messageid}  = "<handler.$ref.$nn.ack${info}quiet\@$gEmailDomain>";
	  $t_h{pr_message} = "ack${infod}-quiet $ref";
	  $t_h{reply_to}   = "$ref-quiet\@$gEmailDomain";
	  $extra_vars->{refreplyto} = "$ref-quiet\@$gEmailDomain";
	  $t_h{subject}    = length($info)?
	       "$gBug#$ref: Info received and FILED only ($subject)":
	       "$gBug#$ref: Acknowledgement of QUIET report ($subject)";
     }
     elsif ($codeletter eq 'M') {
	  $forwardexplain = fill_template('mail/forward_maintonly',
					 );
	  # these are the headers that maintonly messages override
	  $t_h{messageid}  = "<handler.$ref.$nn.ack{$info}maintonly\@$gEmailDomain>";
	  $t_h{pr_message} = "ack${infod}-maintonly $ref";
	  $t_h{reply_to}   = "$ref-maintonly\@$gEmailDomain";
	  $extra_vars->{refreplyto} = "$ref-maintonly\@$gEmailDomain";
	  $t_h{subject}    = length($info)?
	       "$gBug#$ref: Info received for maintainer only ($subject)":
	       "$gBug#$ref: Acknowledgement of maintainer-only report ($subject)";
     }
     else {
	  $forwardexplain = fill_template('mail/forward_normal',
					 );
	  $t_h{messageid}  = "<handler.$ref.$nn.ack${info}\@$gEmailDomain>";
	  $t_h{pr_message} = "ack${infod} $ref";
	  $t_h{reply_to}   = "$ref\@$gEmailDomain";
	  $extra_vars->{refreplyto} = "$ref\@$gEmailDomain";
	  $t_h{subject}    = (defined $info and length($info))?
	       "$gBug#$ref: Info received ($subject)" :
	       "$gBug#$ref: Acknowledgement ($subject)";
     }
     my $body = message_body_template('mail/process_ack',
				      {forwardexplain  => $forwardexplain,
				       resentccexplain => $resentccexplain,
				       thanks          => $thanks,
				       %{$extra_vars}
				      }
				     );
     &sendmessage(create_mime_message(
		       ["X-Loop"      => "$gMaintainerEmail",
			From          => "$gMaintainerEmail ($gProject $gBug Tracking System)",
			To            => $replyto,
			Subject       => $t_h{subject},
			"Message-ID"  => $t_h{messageid},
			"In-Reply-To" => $header{'message-id'},
                        References    => $header{'message-id'},
                        Precedence    => 'bulk',
			"X-$gProject-PR-Message"  => $t_h{pr_message} || "ack $ref",
			"X-$gProject-PR-Package"  => $data->{package},
			"X-$gProject-PR-Keywords" => $data->{keywords},
			# Only have a X-$gProject-PR-Source when we know the source package
			(defined($source_package) and length($source_package))?("X-$gProject-PR-Source" => $source_package):(),
			"Reply-To"                => $t_h{reply_to} || "$ref\@$gEmailDomain",
		       ],$body,[]), '',undef,1);
}

&appendlog;
&finish;

sub overwrite {
    my ($f,$v) = @_;
    open(NEW,">$f.new") || die "$f.new: create: $!";
    print(NEW "$v") || die "$f.new: write: $!";
    close(NEW) || die "$f.new: close: $!";
    rename("$f.new","$f") || die "rename $f.new to $f: $!";
}

sub appendlog {
    my $hash = get_hashname($ref);
    if (!open(AP,">>db-h/$hash/$ref.log")) {
        print DEBUG "failed open log<\n";
        print DEBUG "failed open log err $!<\n";
        die "opening db-h/$hash/$ref.log (li): $!";
    }
    print(AP "\7\n",escape_log(@log),"\n\3\n") || die "writing db-h/$hash/$ref.log (li): $!";
    close(AP) || die "closing db-h/$hash/$ref.log (li): $!";
}

sub finish {
    my ($exit) = @_;
    $exit ||= 0;
    utime(time,time,"db");
    # cleanups are run in an end block now.
    #my ($u);
    #while ($u= $cleanups[$#cleanups]) { &$u; }
    unlink("incoming/P$nn") || die "unlinking incoming/P$nn: $!";
    exit $exit;
}

die "wot no exit";

sub htmllog {
    my ($whatobj,$whatverb,$where,$desc) = @_;
    my $hash = get_hashname($ref);
    open(AP,">>db-h/$hash/$ref.log") || die "opening db-h/$hash/$ref.log (lh): $!";
    print(AP
          "\6\n".
          "<strong>$whatobj $whatverb</strong>".
          ($where eq '' ? "" : " to <code>".html_escape($where)."</code>").
          ":<br>\n". $desc.
          "\n\3\n") || die "writing db-h/$hash/$ref.log (lh): $!";
    close(AP) || die "closing db-h/$hash/$ref.log (lh): $!";
}    

sub stripbccs {
    my $msg = shift;
    my $ret = '';
    my $bcc = 0;
    while ($msg =~ s/(.*\n)//) {
	local $_ = $1;
	if (/^$/) {
	    $ret .= $_;
	    last;
	}
	if ($bcc) {
	    # strip continuation lines too
	    next if /^\s/;
	    $bcc = 0;
	}
	if (/^Bcc:/i) {
	    $bcc = 1;
	} else {
	    $ret .= $_;
	}
    }
    return $ret . $msg;
}

=head2 send_message

     send_message($the_message,\@recipients,\@bcc,$do_not_encode)

The first argument is the scalar message, the second argument is the
arrayref of recipients, the third is the arrayref of Bcc:'ed
recipients.

The final argument turns off header encoding and the addition of the
X-Loop header if true, defaults to false.

=cut


sub sendmessage {
    my ($msg,$recips,$bcc,$no_encode) = @_;
    if (not defined $recips or (!ref($recips) && $recips eq '')
	or @$recips == 0) {
	$recips = ['-t'];
    }
    # This is suboptimal. The right solution is to send headers
    # separately from the rest of the message and encode them rather
    # than doing this.
    $msg = "X-Loop: $gMaintainerEmail\n" . $msg unless $no_encode;
    # The original message received is written out in appendlog, so
    # before writing out the other messages we've sent out, we need to
    # RFC1522 encode the header.
    $msg = encode_headers($msg) unless $no_encode;

    my $hash = get_hashname($ref);
    #save email to the log
    open(AP,">>db-h/$hash/$ref.log") || die "opening db-h/$hash/$ref.log (lo): $!";
    print(AP "\2\n",join("\4",@$recips),"\n\5\n",
          escape_log(stripbccs($msg)),"\n\3\n") ||
        die "writing db-h/$hash/$ref.log (lo): $!";
    close(AP) || die "closing db-h/$hash/$ref.log (lo): $!";

    if (ref($bcc)) {
        shift @$recips if $recips->[0] eq '-t';
        push @$recips, @$bcc;
    }

    send_mail_message(message        => $msg,
		      # Because we encode the headers above, we do not want to encode them here
		      encode_headers => 0,
		      recipients     => $recips);
}

=head2 message_body_template

     message_body_template('mail/ack',{ref=>'foo'});

Creates a message body using a template

=cut

sub message_body_template{
     my ($template,$extra_var) = @_;
     $extra_var ||={};
     my $body = fill_template($template,$extra_var);
     return fill_template('mail/message_body',
			  {%{$extra_var},
			   body => $body,
			  },
			 );
}

=head2 fill_template

     fill_template('mail/foo',{foo=>'bar'});

Calls fill_in_template with a default set of variables and any extras
added in.

=cut

sub fill_template{
     my ($template,$extra_var) = @_;
     $extra_var ||={};
     my $variables = {config => \%config,
		      defined($ref)?(ref    => $ref):(),
		      defined($data)?(data  => $data):(),
		      %{$extra_var},
		     };
     my $hole_var = {'&bugurl' =>
		     sub{"$_[0]: ".
			      'http://'.$config{cgi_domain}.'/'.
				   Debbugs::CGI::bug_links(bug=>$_[0],
							   links_only => 1,
							  );
		    }
		    };
     return fill_in_template(template => $template,
			     variables => $variables,
			     hole_var  => $hole_var,
			    );
}


sub checkmaintainers {
    return if $maintainerschecked++;
    return if !length($data->{package});
    my %maintainerof;
    open(MAINT,"$gMaintainerFile") || die die "maintainers open: $!";
    while (<MAINT>) {
	m/^\n$/ && next;
	m/^\s*$/ && next;
        m/^(\S+)\s+(\S.*\S)\s*\n$/ || die "maintainers bogus \`$_'";
        $a= $1; $b= $2; $a =~ y/A-Z/a-z/;
	# use the package which is normalized to lower case; we do this because we lc the pseudo headers.
        $maintainerof{$a}= $2;
    }
    close(MAINT);
    open(MAINT,"$gMaintainerFileOverride") || die die "maintainers.override open: $!";
    while (<MAINT>) {
	m/^\n$/ && next;
	m/^\s*$/ && next;
        m/^(\S+)\s+(\S.*\S)\s*\n$/ || die "maintainers.override bogus \`$_'";
        $a= $1; $b= $2; $a =~ y/A-Z/a-z/;
	# use the package which is normalized to lower case; we do this because we lc the pseudo headers.
        $maintainerof{$a}= $2;
    }
    close(MAINT);
    my %pkgsrc;
    open(SOURCES,"$gPackageSource") || die "pkgsrc open: $!";
    while (<SOURCES>) {
        next unless m/^(\S+)\s+\S+\s+(\S.*\S)\s*$/;
	($a,$b)=($1,$2);
	$a =~ y/A-Z/a-z/;
	$pkgsrc{$a} = $b;
    }
    close(SOURCES);
    my $anymaintfound=0; my $anymaintnotfound=0;
    for my $p (split(m/[ \t?,():]+/,$data->{package})) {
        $p =~ y/A-Z/a-z/;
	$p =~ /([a-z0-9.+-]+)/;
	$p = $1;
	next unless defined $p;
	if (defined $gSubscriptionDomain) {
	    if (defined($pkgsrc{$p})) {
		push @addsrcaddrs, "$pkgsrc{$p}\@$gSubscriptionDomain";
	    } else {
		push @addsrcaddrs, "$p\@$gSubscriptionDomain";
	    }
	}
        if (defined($maintainerof{$p})) {
	    print DEBUG "maintainer add >$p|$maintainerof{$p}<\n";
            my $addmaint= $maintainerof{$p};
            push(@maintaddrs,$addmaint) unless
                $addmaint eq $replyto || grep($_ eq $addmaint, @maintaddrs);
            $anymaintfound++;
        } else {
	    print DEBUG "maintainer none >$p<\n";
	    push(@maintaddrs,$gUnknownMaintainerEmail) unless $anymaintnotfound;
            $anymaintnotfound++;
            last;
        }
    }

    if (defined $data->{owner} and length $data->{owner}) {
        print DEBUG "owner add >$data->{package}|$data->{owner}<\n";
        my $addmaint = $data->{owner};
        push(@maintaddrs, $addmaint) unless
            $addmaint eq $replyto or grep($_ eq $addmaint, @maintaddrs);
    }
}

=head2 bug_list_forward

     bug_list_forward($spool_filename) if $codeletter eq 'L';


Given the spool file, will forward a bug to the per bug mailing list
subscription system.

=cut

sub bug_list_forward{
     my ($bug_fn) = @_;
     # Read the bug information and package information for passing to
     # the mailing list
     my ($bug_number) = $bug_fn =~ /^L(\d+)\./;
     my ($bfound, $data)= lockreadbugmerge($bug_number);
     my $bug_fh = IO::File->new("incoming/P$bug_fn",'r') or die "Unable to open incoming/P$bug_fn $!";

     local $/ = undef;
     my $bug_message = <$bug_fh>;
     my ($bug_address) = $bug_message =~ /^Received: \(at ([^\)]+)\) by/;
     my ($envelope_from) = $bug_message =~ s/\nFrom\s+([^\s]+)[^\n]+\n/\n/;
     if (not defined $envelope_from) {
	  # Try to use the From: header or something to set it 
          ($envelope_from) = $bug_message =~ /\nFrom:\s+(.+?)\n/;
	  # Kludgy, and should really be using a full scale header
	  # parser to do this.
	  $envelope_from =~ s/^.+?<([^>]+)>.+$/$1/;
     }
     my ($header,$body) = split /\n\n/, $bug_message, 2;
     # Add X-$gProject-PR-Message: list bug_number, package name, and bug title headers
     $header .= qq(\nX-$gProject-PR-Message: list $bug_number\n).
	  qq(X-$gProject-PR-Package: $data->{package}\n).
	       qq(X-$gProject-PR-Title: $data->{subject})
	       if defined $data;
     print STDERR "Tried to loop me with $envelope_from\n"
	  and exit 1 if $envelope_from =~ /\Q$gListDomain\E|\Q$gEmailDomain\E/;
     print DEBUG $envelope_from,qq(\n);
     # If we don't have a bug address, something has gone horribly wrong.
     print STDERR "Doesn't match: $bug_address\n" and exit 1 unless defined $bug_address;
     $bug_address =~ s/\@.+//;
     print DEBUG "Sending message to bugs=$bug_address\@$gListDomain\n";
     print DEBUG $header.qq(\n\n).$body;
     send_mail_message(message        => $header.qq(\n\n).$body,
		       recipients     => ["bugs=$bug_address\@$gListDomain"],
		       envelope_from  => $envelope_from,
		       encode_headers => 0,
		      );
     unlink("incoming/P$bug_fn") || die "unlinking incoming/P$bug_fn: $!";
     exit 0;
}
